        PAGE 64
        .CREF
        .Z80
;
;       ****************************************************************
;       *                                                              *
;       *  ACC-BIOS fuer den AC1-2010                                  *
;       *                                                              *
;       *  10.04.2024                                                  *
;       *                                                              *
;       *  LW A: Praecitronic RAM-Disk 256 KByte                       *
;       *  LW B: Floppylaufwerk MFM1.6 780 KByte                       *
;       *  Tastatur mit Interruptsteuerung                             *
;       *  Bildschirmausgabe nach ACC-Dresden                          *
;       *  automatische RAM-Disk Groessenerkennung 256K bis 2048K      *
;       *  Umschaltung Diskettenformat 780K/800K mit dem DISK-Befehl   *
;       *                                                              *
;       ****************************************************************
;
FLOPPY  EQU  1            ;BIOS mit RamDisk A: und Floppy B:
;
        IF1
        .PRINTX 'PASS 1'
        IFDEF FLOPPY
        .PRINTX 'BIOS mit RamDisk A: und Floppy B:'
        ELSE
        .PRINTX 'BIOS mit RamDisk A: (kein Floppy-LW)'
        ENDIF
        ENDIF
;
        IF2
        .PRINTX 'PASS 2'
        ENDIF
;
IOBYTE  EQU  0003H
LWBYTE  EQU  0004H        ;LW (Bit0-3) + USER Nr.(Bit4-7)
GETCO   EQU  07FDH        ;Ruecksprung in den AC1-Monitor
IFDEF FLOPPY
CCP     EQU  0D000H       ;Startadresse CCP = 0D000h mit Floppy
ELSE
CCP     EQU  0E000H       ;Startadresse CCP = 0E000h ohne Floppy
ENDIF
BDOS    EQU  CCP+806H     ;Startadresse BDOS
BIOS    EQU  CCP+1600H    ;Startadresse BIOS
;
PIO1AC  EQU  06H          ;PIO1 Port A Control
PIO1AD  EQU  04H          ;PIO1 Port A Data
PIO1BC  EQU  07H          ;PIO1 Port B Control
PIO1BD  EQU  05H          ;PIO1 Port B Data
MAP     EQU  1EH          ;Seitenschaltung nach FA
;
AC1MOD  EQU  0            ;AC1 - Mode
CPMMOD  EQU  1            ;CP/M - Mode
;
        .PHASE BIOS
;
        JP   BOOT
BIOSWB: JP   WBOOT
        JP   CONST
        JP   CONIN
        JP   CONOUT
        JP   LIST
        JP   PUNCH
        JP   READER
        JP   HOME
        JP   SELDSK
        JP   SETTRK
        JP   SETSEC
        JP   SETDMA
        JP   READ
        JP   WRITE
        JP   LISTST
        JP   SECTRN
;
        DEFM 'CPMAC'      ;fuer PC/BC Progr.
        JP   EXIT         ;EXIT-Kommando
        IFDEF FLOPPY
        JP   DISK         ;DISK-Umschaltung 780K <--> 800K
        ELSE
        JP   0000H
        ENDIF
        JP   0000H        ;3. User-Befehl
;
SELDSK: LD   HL,0         ;HL=0 --> kein LW
        LD   A,(NDISK)    ;Anzahl der LW
        LD   E,A
        LD   A,C          ;C = akt. LW
        CP   E
        RET  NC           ;akt. LW >= NDISK
        LD   (DRIVE),A    ;Merker fuer akt. LW
        LD   L,C
        ADD  HL,HL
        ADD  HL,HL
        ADD  HL,HL
        ADD  HL,HL
        LD   DE,DPH
        ADD  HL,DE        ;zugeordneter DPH --> HL
        RET
;
SECTRN: LD   H,0          ;CP/A-Routine fuer 8 Bit
        LD   L,C
        INC  HL
        RET
;
SETSEC: LD   A,C
        LD   (SECTOR),A
        RET
;
HOME:   LD   C,0
SETTRK: LD   A,C
        LD   (TRACK),A
        RET
;
SETDMA: LD   (DMA),BC
        RET
;
PLATZ:  DEFS BIOS+80H-PLATZ-5,0
;
TZ:     DEFB 0            ;Fuellstand Tastaturpuffer
TZOUT:  DEFW TAPU         ;Ausgabezeiger
TZIN:   DEFW TAPU         ;Eingabezeiger
TAPU:   DEFS 8,0          ;Tastatur-Ringpuffer
;
IV:     DEFW INTA         ;Int.-Vektor fuer CONIN (PIO)
;
        .PRINTX '--> BOOT'
$INCLUDE BOOT.INC
;
IFDEF FLOPPY
        .PRINTX '--> DISKSET'
$INCLUDE DISKSET.INC
;
        .PRINTX '--> FLOPPY'
$INCLUDE FLOPPY.INC
ENDIF
;
        .PRINTX '--> RAMDISK'
$INCLUDE RAMDISK.INC
;
        .PRINTX '--> CONSOLE'
$INCLUDE CONSOLE.INC
;
WRITE:  LD   A,(DRIVE)
        CP   0
        JP   Z,WRRDSK
        IFDEF FLOPPY
        CP   1
        JP   Z,WRFLO
        ENDIF
        LD   A,1          ;ERROR
        RET
;
READ:   LD   A,(DRIVE)
        CP   0
        JP   Z,RDRDSK
        IFDEF FLOPPY
        CP   1
        JP   Z,RDFLO
        ENDIF
        LD   A,1          ;ERROR
        RET
;
LIST:   JP   CONOUT
;
LISTST: LD   A,0          ;nicht bereit
        RET
;
PUNCH:  JP   CONOUT
;
READER: JP   CONIN
;
DERROR: LD   HL,TEXT2
        CALL OUTTXT
ERROR:  LD   HL,TEXT3
        CALL OUTTXT
        IFDEF FLOPPY
        CALL DERR1        ;Floppy-Motor ausschalten
        ENDIF
        JP   WBOOT
;
OUTTXT: PUSH AF
        PUSH BC
TXTLOP: LD   A,(HL)
        CP   0
        JR   Z,TXTEND
        LD   C,A
        CALL CONOUT
        INC  HL
        JR   TXTLOP
TXTEND: POP  BC
        POP  AF
        RET
;
TEXT1:  DEFB 0CH,1BH,03H,0BH
        DEFM ' CP/M 2.2  Version 1.50 fuer den AC1-2010 '
        DEFB 0DH,0AH,0AH,0AH
        DEFM 'A:RAM-Disk '
        DEFB 00H
TEXT1A: DEFM "256K"
        DEFB 00H
TEXT1B: DEFM "512K"
        DEFB 00H
TEXT1C: DEFM "1024K"
        DEFB 00H
TEXT1D: DEFM "2048K"
        DEFB 00H
IFDEF FLOPPY
TEXT1E: DEFM '  B:Floppy 780K  '
ELSE
TEXT1E: DEFM ' (System-Track=255)  '
ENDIF
        DEFB 0DH,0AH,0AH,00H
TEXT2:  DEFB 0DH,0AH
        DEFM 'Disk-'
        DEFB 00H
TEXT3:  DEFM 'ERROR'
        DEFB 0DH,0AH,07H,00H
;
DPH:    DEFW 0            ;RAM-Disk 256K
        DEFW 0
        DEFW 0
        DEFW 0
        DEFW DIRBF
        DEFW DPB0
        DEFW 0            ;weil nichtwechselbares Medium
        DEFW ALL0
;
IFDEF FLOPPY
        DEFW 0            ;Floppy 780K
        DEFW 0
        DEFW 0
        DEFW 0
        DEFW DIRBF
        DEFW DPB1
        DEFW CHK1
        DEFW ALL1
ENDIF
;
;Disk-Parameter-Block  RAM-Disk 256K
;
DPB0:   DEFW 64           ;64 Sectoren/Track (Rec/Trk) = 8 Kbyte/Track
        DEFB 3            ;Blockgroesse = 1 Kbyte
        DEFB 7
        DEFB 0            ;8-Bit Blockadressen im FCB/DIR, da weniger als 255 Bloecke
        DEFW 247          ;248 * 1 Kbyte-Bloecke  -> 256k-8k(System)=248k(CP/M) ( - 2k(DIR) -> 246k (NETTO f. Daten))
        DEFW 63           ;64 DIR - Eintaege
        DEFW 0C0H         ;2 DIR-Bloecke
        DEFW 0            ;weil nichtwechselbares Medium
        DEFW 1            ;8 Kbyte System / Track-Offset
;
        DEFW 0
;
;Disk-Parameter-Block  Floppy 780K
;
IFDEF FLOPPY
DPB1:   DEFW 80           ;80 Sectoren/Track
        DEFB 4            ;Blockgroesse = 2 Kbyte
        DEFB 15
        DEFB 0
        DEFW 389          ;390 * 2 Kbyte - Bloecke
        DEFW 127          ;128 DIR - Eintraege
        DEFW 0C0H         ;pro DIR-Block 1Bit gesetzt
        DEFW 32
        DEFW 2            ;20 Kbyte System
;
        DEFW 0
ENDIF
;
;Disk-Parameter-Bloecke fuer RAM-Disk 512 / 1024 / 2048 KByte
;
;RAM-Disk 512 KByte  LW A:
DP512:  DEFW 64           ;64 Sectoren/Track (Rec/Trk) = 8 Kbyte/Track
        DEFB 4            ;Blockgroesse = 2 Kbyte
        DEFB 15
        DEFB 1            ;8-Bit Blockadressen im FCB/DIR, da weniger als 255 Bloecke
        DEFW 251          ;252 * 2 Kbyte-Bloecke  -> 512k-8k(System)=504k(CP/M) ( - 2k(DIR) -> 502k (NETTO f. Daten))
        DEFW 63           ;64 DIR - Eintaege
        DEFW 080H         ;1 DIR-Block
        DEFW 0            ;weil nichtwechselbares Medium
        DEFW 1            ;8 Kbyte System / Track-Offset
;
;RAM-Disk 1024 KByte  LW A:
DP1024: DEFW 64           ;64 Sectoren/Track (Rec/Trk) = 8 Kbyte/Track
        DEFB 4            ;Blockgroesse = 2 Kbyte
        DEFB 15
        DEFB 0            ;16-Bit Blockadressen im FCB/DIR, da mehr als 255 Bloecke
        DEFW 507          ;508 * 2 Kbyte-Bloecke  -> 1024k-8k(System)=1016k(CP/M) ( - 4k(DIR) -> 1012k (NETTO f. Daten))
        DEFW 127          ;128 DIR - Eintraege -> bei 64 DIR-Eintraegen bekommt man die RAM-Disk nicht voll
        DEFW 0C0H         ;2 DIR-Bloecke
        DEFW 0            ;weil nichtwechselbares Medium
        DEFW 1            ;8 Kbyte System / Track-Offset
;
;RAM-Disk 2048 KByte  LW A:
DP2048: DEFW 64           ;64 Sectoren/Track (Rec/Trk) = 8 Kbyte/Track
        DEFB 4            ;Blockgroesse = 2 Kbyte
        DEFB 15
        DEFB 0            ;16-Bit Blockadressen im FCB/DIR, da mehr als 255 Bloecke
        DEFW 1019         ;1020 * 2 Kbyte-Bloecke  -> 2048k-8k(System)=2040k(CP/M) ( - 8k(DIR) -> 2032k (NETTO f. Daten))
        DEFW 255          ;256 DIR - Eintraege
        DEFW 0F0H         ;4 DIR-Bloecke
        DEFW 0            ;weil nichtwechselbares Medium
        DEFW 1            ;8 Kbyte System / Track-Offset
;
; CP/M verlassen
;
EXIT:   DI
        LD   SP,2000H
        LD   HL,CCP+7
        LD   (HL),0       ;Kommandopuffer loeschen
        LD   A,AC1MOD
        OUT  (MAP),A
        LD   A,3          ;verbiete Interrupt
        OUT  (PIO1AC),A
        LD   A,4FH        ;PIO - Mode 1
        OUT  (PIO1AC),A
        LD   HL,103FH
        LD   (1800H),HL   ;neue Kursorposition
        LD   A,0FH
EXIT1:  LD   (HL),20H     ;letzte BS-Zeile loeschen
        DEC  HL
        CP   H
        JR   NZ,EXIT1
        JP   GETCO        ;Ruecksprung in den AC1-Monitor
;
; RAM-Zellen
;
IFDEF FLOPPY
NDISK:  DEFB 2            ;Anzahl der LW
ELSE
NDISK:  DEFB 1            ;Anzahl der LW
TRACK:  DEFB 0            ;FLOPPY.INC ist ausgeblendet
SECTOR: DEFB 1            ;TRACK muss dort bleiben, weil
DMA:    DEFW 0080H        ;es in CTAB/CTAB2 verwendet wird
ENDIF
DRIVE:  DEFB 0            ;Merker aktuelles LW
;
DIRBF:  DEFS 80H,0
ALL0:   DEFS 128,0        ;Anzahl der Bit's = Anzahl der
IFDEF FLOPPY
ALL1:   DEFS 50,0         ;Bloecke, die zu verwalten sind
CHK1:   DEFS 32,0         ;= CKS im DPB = DIR-Entrys/4
ENDIF
;
SAVSTK: DEFS 80H,0
BIOSTK:
;
        DEFM 'End of BIOS'
;
        .DEPHASE
        END
;
